title: ERC 792
subtitle: The standard for Arbitra(tor/ble) smart contracts.
class: animation-fade
layout: true

.bottom-bar[{{title}} - {{subtitle}}]

---

class: impact
background-image: url(https://ucdffce48006b4016fbfa4d30b1b.previews.dropboxusercontent.com/p/thumb/AAMQ157A_a8G1d24qPufyJHERn7n64QPduRLR4a-YjdyTV6zzDv8RNrr2a5iE1rBK_cB9CNr8FiXO-RiKupjIYCPCHQHsgQqIeFHIWyFYpXIT3I2RXK_hNhEFOF0LkQAplwKvCpkXoeZVaw60Oj8N-z5FkZvDeT4Ha1qvBUI6GRNNU1VwMPZCm9pzdmC9KrViMAxmRjvtb83RtjzBu4DO3xoY2IEVGxlMV6LvNI2VXnU6g/p.jpeg?size=2048x1536&size_mode=3)

# {{title}}

## {{subtitle}}

???
Welcome everyone, today we will go over Kleros' arbitration ethereum improvement proposal, which aims to standardize the way the demand side of dispute resolution (the arbitrable side) interacts with the supply side (the arbitration side).

The aim of the proposal is to provide a minimal interface by which any two conforming contracts can interact with each other to execute dispute resolution, regardless of implementation details. This way, dapps and their smart contracts can plug and play any contract that implements the standard. This will create a competitive market for arbitrators, which in turn will lead to better services, at lower prices, for consumers, all without additional development costs for any of the parties involved.

By the end of the presentation, you should have a clear understanding of how to develop smart contracts that adhere to the standard. OK, so let's get started.

---

# A Primer on Blockchains

## They:

--

- Are distributed data stores that can be used to store any sort of data .alt.thin.small[(e.g. Bitcoin stores balances for a bunch of addresses.)].

--

- Can be edited as long as certain rules are followed .alt.thin.small[(e.g. No double spending, no spending of others' funds, etc.)].

--

- Guarantee provable data integrity, commonly through different consensus protocols based on `cryptography + economics = cryptoeconomics`.

---

# Smart? Contracts

## They:

--

- Are stateful programs that get sent in a special deployment `TX` and become immutable.

--

- Have their own address to which you can send `TX`s with some input data and value and have some logic executed on-chain.

--

- Guarantee provable data and state transition integrity through the chain they live on, in our case, Ethereum.

???
The simplest way of picturing them in Ethereum is as special wallets that expose an `instance` of a `class` to which you can send special transactions to. These special transactions execute your specified methods of the `instance`, with your specified inputs, and can modify its state. You can also read this state at any time. These `instance`s have access to all sorts of `TX` and chain metadata and can also interact with other `instance`s of other `class`es and even transfer ETH.

They are incredibly powerful because they allow developers to easily create distributed applications and systems on top of an existing chain like Ethereum, without having to worry about all the internals like networking and consensus protocols. These applications can be anything from new currencies like `ERC20` tokens to multiplayer games.

In distributed applications, databases become contract state, and APIs become contract methods.

---

# A Note on Smart Contract Design

--

.col-6[

### Traditional Back Ends:

- Do most of the heavy lifting for the front end, because computation is cheap and faster than on the client.

.responsive[![Traditional Back End](https://apexminecrafthosting.com/wp-content/uploads/2015/10/apex_server8.gif)]

]

--

.col-6[

### Smart Contracts Back Ends:

- Leave most of the heavy lifting for the front end, because computation is expensive and slower than on the client.

.responsive[![Smart Contract Back End](https://www.conceptdraw.com/How-To-Guide/picture/fully-connected-network-topology/Network-Topology-Fully-Connected.png)]

]

???
This is an inherent property of consensus based systems that involve lots of parties.

Improvements will always be made, but it will also always be cheaper to execute code on the front end.

Abstractions can be made to make front end developers' lifes easier. `kleros-api` does just that for ERC 792 contracts.

---

# Arbitrable? Arbitrator?

.full-screen[![Example Arbitrable Escrow Payment UML Diagram](https://github.com/kleros/kleros-presentations/blob/master/assets/images/example-arbitrable-escrow-flow.png?raw=true)]

???
So now you might be wondering how the words arbitrable and arbitrator can be turned into smart contracts. Well, in english and in the context of dispute resolution, the arbitrable party is the one that demands the service of arbitration from the arbitrator, the supplier of the service. Arbitrators give rulings to arbitrable parties. The smart contract standard is designed to emulate this construct in code. This is a diagram of the flow between clients, arbitrable contract, and arbitrator contract for the simple use case of an arbitrable escrow payment. We will actually implement this example in code later on in the presentation.

To start, the client creates a payment which involves sending the funds to be escrowed and agreeing on the potential arbitration. If there are no issues between the parties involved during the time out, the payment can be executed, otherwise a dispute is raised on the arbitrator via the arbitrable contract. The parties can start submitting evidence, and eventually the arbitrator gives a ruling with a time out for appeals. Once the time out passes, the arbitrator can execute the ruling on the arbitrable contract.

---

# ERC 792 Arbitrator: Interface

.small[

```sol
contract Arbitrator {
    function createDispute(
        uint _choices, bytes _extraData
    ) public requireArbitrationFee(_extraData) payable returns(uint disputeID) {};

    function arbitrationCost(bytes _extraData) public view returns(uint fee);

    function appeal(
        uint _disputeID, bytes _extraData
    ) public requireAppealFee(_disputeID,_extraData) payable {
        emit AppealDecision(_disputeID, Arbitrable(msg.sender));
    }

    function appealCost(uint _disputeID, bytes _extraData) public view returns(uint fee);

    function appealPeriod(uint _disputeID) public view returns(uint start, uint end) {}

    function disputeStatus(uint _disputeID) public view returns(DisputeStatus status);

    function currentRuling(uint _disputeID) public view returns(uint ruling);
}
```

]

???
These are all the methods that an arbitrable contract should need to execute a dispute resolution flow. Let's go over each method one by one.

Note that we've removed the Doxygen comments for simplicity.

We have:

- A way of creating a dispute.
- A way of getting the cost of creating a dispute.
- A way of appealing a ruling.
- A way of getting the cost of appealing a ruling.
- (Optional) A way to see the time window you have for appealing a ruling (Useful for multi-party fee payments.).
- A way of getting a dispute's status.
- A way of getting a dispute's current ruling (You might want to appeal it before it's final.).

---

# ERC 792 Arbitrator: Types and Events

.small[

```sol
contract Arbitrator {
    enum DisputeStatus { Waiting, Appealable, Solved }

    event DisputeCreation(uint indexed _disputeID, Arbitrable indexed _arbitrable);

    event AppealPossible(uint indexed _disputeID, Arbitrable indexed _arbitrable);

    event AppealDecision(uint indexed _disputeID, Arbitrable indexed _arbitrable);
}
```

]

???
For types we just have an enum that tells you if the dispute is waiting for a ruling, in an appeal period, or already resolved.

Events are like immutable logs that can be efficiently indexed and queried by clients.

We have:

- One for when a dispute is created.
- One for when a dispute's ruling becomes appealable.
- One for when a dispute's ruling is appealed.

---

# ERC 792 Arbitrable: Interface and Events

.small[

```sol
contract Arbitrable {
    event Ruling(Arbitrator indexed _arbitrator, uint indexed _disputeID, uint _ruling);

    function rule(uint _disputeID, uint _ruling) public onlyArbitrator;
}
```

]

### Evidence Standard:

[Link](link)

### Composed Arbitrable Contracts:

[Link](link)

???
This is all that an arbitrator contract should need to execute a dispute resolution flow.

We have a way for the arbitrator to give a final ruling and an event to notify clients when that happens.

As you can see, this interface is much simpler than the arbitrator one. But, that doesn't mean that arbitrable contracts are always easier to implement. There is a lot of complex logic involved with evidence submission, multi-party and crowdinsured fee payments, and then there is what the contract does after a ruling is given. That's why we have developed a few things on top of ERC 792 to make it easier for third parties to create arbitrable contracts.

We have an evidence event standard for submission and verification of off-chain data to serve as context, like the ruling question and possible decisions, and evidence for a dispute. We also have a set of composable arbitrable contracts that each implement a piece of the puzzle and can be used through inheritance or a proxy model. We will actually give an example of that later today.

---

## ERC 792 Arbitrator: Simple Example

### Centralized Arbitrator: Set Up

.small[

```sol
contract CentralizedArbitrator is Arbitrator {
    struct Dispute {
        Arbitrable arbitrated;
        uint choices;
        uint fee;
        uint ruling;
        DisputeStatus status;
    }

    address public owner = msg.sender;
    uint arbitrationPrice;
    uint constant NON_PAYABLE_VALUE = (2 ** 256 - 2) / 2;
    Dispute[] public disputes;

    modifier onlyOwner {require(msg.sender == owner, "Can only be called by the owner."); _;}

    constructor(uint _arbitrationPrice) public {arbitrationPrice = _arbitrationPrice;}
}
```

]

???
OK, so now let's go over how one might implement an actual working arbitrator. We'll go over the code of a simple centralized arbitrator where one party can execute rulings unanimously and without possibility of appeals, and then we'll briefly go over more complex architectures using our very own Kleros Liquid as an example.

First, we inherit from the arbitrator interface. Then we define a struct type for our disputes and all the storage variables we'll need. This is state that persists and is shared between different `TX` executions.

We have:

- The address of the owner to authorize certain actions like giving rulings.
- The arbitration price so we can change it like a setting. Note that we did not make it public, which automatically creates a getter, because `arbitrationCost` will already act as a getter.
- A non payable value, more than the total supply of ETH, to block appeals without having to change the standard interface.
- A list of disputes.

We then define a modifier, this is basically like a very simple implementation of function decorators where the underscore gets replaced with the function's actual body.

Finally, we define the constructor which only takes and sets the arbitration price.

---

## ERC 792 Arbitrator: Simple Example

### Centralized Arbitrator: Setters and Getters

.small[

```sol
contract CentralizedArbitrator is Arbitrator {
    function setArbitrationPrice(uint _arbitrationPrice) public onlyOwner {
        arbitrationPrice = _arbitrationPrice;
    }
    function arbitrationCost(bytes _extraData) public view returns(uint fee) {
        return arbitrationPrice;
    }
    function appealCost(uint _disputeID, bytes _extraData) public view returns(uint fee) {
        return NON_PAYABLE_VALUE;
    }
    function disputeStatus(uint _disputeID) public view returns(DisputeStatus status) {
        return disputes[_disputeID].status;
    }
    function currentRuling(uint _disputeID) public view returns(uint ruling) {
        return disputes[_disputeID].ruling;
    }
}
```

]

???
We have:

- A setter for the arbitration price. Note that it has the `onlyOwner` modifier so that not anyone can change the price.
- The standard getter for arbitration cost.
- The standard getter for appeal cost, always returning a non payable value because this contract does not support appeals.
- The standard getter for a dispute's status.
- The standard getter for a dispute's current ruling.

The modifier `view` tells the compiler that those functions don't modify state, only read from it, which means they can be executed on the Ethereum virtual machine without a transaction being sent, which also means no gas fees for the caller. You can get pretty smart with views and implement complex query functions that support traditional API patterns like pagination, sorting, and filtering.

---

## ERC 792 Arbitrator: Simple Example

### Centralized Arbitrator: Creating and Ruling Disputes

.extra-small[

```sol
contract CentralizedArbitrator is Arbitrator {
    function createDispute(uint _choices, bytes _extraData) public payable returns(uint disputeID)  {
        super.createDispute(_choices, _extraData);
        disputeID = disputes.push(DisputeStruct({
            arbitrated: Arbitrable(msg.sender),
            choices: _choices,
            fee: msg.value,
            ruling: 0,
            status: DisputeStatus.Waiting
        })) - 1;
        emit DisputeCreation(disputeID, Arbitrable(msg.sender));
    }

    function giveRuling(uint _disputeID, uint _ruling) public onlyOwner {
        DisputeStruct dispute = disputes[_disputeID];
        require(_ruling <= dispute.choices, "Invalid ruling.");
        require(dispute.status != DisputeStatus.Solved, "The dispute has already been ruled.")

        dispute.ruling = _ruling;
        dispute.status = DisputeStatus.Solved;
        msg.sender.send(dispute.fee);
        dispute.arbitrated.rule(_disputeID, _ruling);
    }
}
```

]

???
Now we need a way to create and solve disputes.

For creating disputes, first, we call the parent contract's implementation to enforce the arbitration cost. Then, we insert a new dispute into the list and save it's ID, which is just its index. Note the payable modifier which means that the function can receive ETH.

We set:

- `arbitrated`: To the sender.
- `choices`: To the passed in parameter.
- `fee`: To the amount of ETH, in Wei, sent with the `TX`.
- `ruling`: To 0, the initial value.
- `status`: To waiting.

Then we emit the standard event and we are done.

The function for giving a ruling is also very simple. First, we define a reference to the specified dispute for easy reference. We check that the ruling is within the range of choices and that the dispute has not already been ruled. If any of these conditions fail, we revert and the `TX` has no effect. We then save the dispute's ruling, set the dispute's status to solved, send the fee to the caller, and call rule on the arbitrable contract. Note that the caller is always the owner because the function has the `onlyOwner` modifier.

And there you have it, a fully working, ERC 792 standard compliant arbitrator.

---

## ERC 792 Arbitrator: Complex Example

### Kleros Liquid:

--

- Has a tree shaped hierarchy of subcourts, where jurors can choose paths to participate in.

--

- Supports subcourt appeals and appeals to higher subcourts.

--

- Supports governance for global and subcourt specific settings like fees and time outs.

--

- Implements the evidence standard and another set of events designed to power a rich client notification system.

--

[Link](link)

???
This is our second implementation of the Kleros whitepaper and is just one more example of how diverse arbitrator logic can be. The ERC has been designed in a way that makes it agnostic to all of these implementation details. We are excited to see what kind of other arbitrators the community comes up with.

---

## ERC 792 Arbitrator: Other Examples

--

- An arbitrator with a back up arbitrator in case it doesn't rule on time.

--

- An arbitrator that appeals to another arbitrator.

--

- An arbitrator that assigns weights to different arbitrators and computes an average ruling.

--

- Any sort of complex system or DAO that gives rulings.

.responsive[![Kleros Tech Stack](https://datavizcatalogue.com/methods/images/top_images/SVG/network_diagram.svg)]

???
The possibilities are really endless here.

---

## ERC 792 Arbitrable: Simple Example

### Two Party Arbitrable Escrow Payment: Storage and Modifiers

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is Arbitrable {
    address public sender;
    address public receiver;
    uint public value;
    bytes public extraData;
    Arbitrator public arbitrator;
    uint public disputeID;
    bool public disputed;
    bool public appealed;
    bool public executed;
    uint public createdAt;
    uint public timeOut;

    modifier onlySenderOrReceiver{
        require(msg.sender == sender || msg.sender == receiver, "Can only be called by the sender or the receiver.");
        _;
    }
}
```

]

???
OK, now let's go over how one might implement a simple arbitrable escrow payment contract. The contract should hold the value of the payment until the specified time out passes and in the case of a dispute, should wait for the ruling to determine who to send the value to. A possible real life use case of this contract could be paying for goods on an e-commerce site. The seller can send the goods knowing the money is there, but if the goods don't arrive on time, or do not meet the specifications, the buyer can raise a dispute and get the money back.

OK, so first, we inherit from the arbitrable interface and define some storage and a modifier that will come in handy later.

We have:

- The sender of the payment.
- The receiver of the payment.
- The value being paid.
- The extra data sent to the arbitrator in case of a dispute or appeal.
- The arbitrator to use in case of a dispute.
- A placeholder for the possible dispute ID.
- Wether the payment has been disputed or not.
- Wether a ruling has been appealed yet.
- Wether the payment has already been executed.
- The time the contract was deployed at.
- The time to wait before allowing parties to execute the payment if not disputed.

The modifier is quite simple and just enforces that the caller is either the sender or the receiver.

---

## ERC 792 Arbitrable: Simple Example

### Two Party Arbitrable Escrow Payment: Payment Lifecycle

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is Arbitrable {
    constructor(address _receiver, bytes _extraData, Arbitrator _arbitrator, uint _timeOut, string _metaEvidence) public payable {
        sender = msg.sender;
        receiver = _receiver;
        value = msg.value;
        extraData = _extraData;
        arbitrator = _arbitrator;
        createdAt = now;
        timeOut = _timeOut;
        emit MetaEvidence(0, _metaEvidence);
    }
    function raiseDispute() public payable onlySenderOrReceiver {
        emit Dispute(arbitrator, arbitrator.createDispute.value(msg.value)(2, extraData), 0);
    }
    function submitEvidence(string _evidence) public onlySenderOrReceiver {
        require(disputed, "The payment has to be disputed.");
        require(!appealed, "The payment can not be appealed.");
        emit Evidence(arbitrator, disputeID, msg.sender, _evidence);
    }
    function appeal() public payable onlySenderOrReceiver {
        arbitrator.appeal.value(msg.value)(disputeID, extraData);
        if (!appealed) appealed = true;
    }
}
```

]

???
Now, let's go over the main lifecyle functions of the payment and potential dispute.

We have the constructor which we expect to be called by the sender. It justs saves all the initial data and emits the `MetaEvidence` event from the evidence standard to provide context for the potential dispute. Then, we have a function that raises the dispute on the arbitrator, forwarding all value, and raises the `Dispute` event from the evidence standard to notify clients that a dispute was raised and to link it to the meta evidence. Following that is the function for submitting evidence, again using the evidence standard, in which we check that the dispute is disputed, but not appealed, because we don't want jurors in appeal rounds to have different information than previous ones. Finally, we have a function that calls appeal on the arbitrator just like with dispute creation.

---

## ERC 792 Arbitrable: Simple Example

### Two Party Arbitrable Escrow Payment: Execution

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is Arbitrable {
    function executePayment() public onlySenderOrReceiver {
        require(now - createdAt > timeOut, "The timeout time has not passed yet.");
        require(!disputed, "The payment is disputed.");
        require(!executed, "The payment was already executed.");
        executed = true;
        receiver.send(value);
    }
    function executeRuling(uint _disputeID, uint _ruling) internal {
        require(disputed, "The payment is not disputed.");
        require(_disputeID == disputeID, "Wrong dispute ID.");
        require(!executed, "The payment was already executed.");
        executed = true;
        if (_ruling == 2) receiver.send(value);
        else sender.send(value);
        emit Ruling(arbitrator, disputeID, _ruling);
    }
}
```

]

???
To finish up the contract we have a way of executing the payment if it was not disputed during the time out and a way to execute the arbitrator's ruling if it was.

---

## ERC 792 Arbitrable: Complex Example

### Two Party Arbitrable Escrow Payment: New Features

???
Notice how most of the contract's code was logic related to the ERC and the evidence standard and not application specific. Now, imagine how much more complex it can get if you implement more viable contracts. A much more useful version of this contract could have:

--

- Multiple sending and/or receiving parties.

--

- Shared and collaterized fees.

--

- Support for insurance providers to cover fees and collect rewards.

--

- Different evidence submission rules.

--

- Multiple payments. I.e. have the contract store payments instead of it being the payment.

???
One of our main goals at Kleros is to make arbitrable contract creation and integration with arbitrators as simple as possible. Which is why we are working on a set of composable contracts that each implement a piece of the puzzle and can be used through inheritance or a proxy model. Now, we'll demonstrate how you can implement a version of this escrow contract with all the features we just talked about and with a fraction of the code.

---

## ERC 792 Arbitrable: Complex Example

### Two Party Arbitrable Escrow Payment: Set Up

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is MultiPartyInsurableArbitrableAgreementsBase {
    struct Payment {
        uint value;
        uint createdAt;
        uint timeOut;
    }
    event PaymentExecuted(bytes32 indexed _paymentID, address indexed _sender, address indexed _receiver, uint _value);
    mapping(bytes32 => Payment) public payments;
    constructor(
        Arbitrator _arbitrator,
        bytes _arbitratorExtraData,
        address _feeGovernor,
        uint _stake
    ) public MultiPartyInsurableArbitrableAgreementsBase(_arbitrator, _arbitratorExtraData, _feeGovernor, _stake) {}
}
```

]

???
OK, set up, we inherit from the composed contract, we define a struct for payments, an event for when payments are executed, a mapping to store the payments, and a constructor that just sets all the initial values.

---

## ERC 792 Arbitrable: Complex Example

### Two Party Arbitrable Escrow Payment: Creating Payments

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is MultiPartyInsurableArbitrableAgreementsBase {
    function createPayment(
        bytes32 _paymentID, string _metaEvidence, address _to, uint _arbitrationFeesWaitingTime, Arbitrator _arbitrator, uint _timeOut
    ) external payable {
        require(msg.value > 0, "Payment must be more than zero.");
        address[] memory _parties = new address[](2);
        _parties[0] = msg.sender;
        _parties[1] = _to;
        _createAgreement(
            _paymentID,
            _metaEvidence,
            _parties,
            2,
            new bytes(0),
            _arbitrationFeesWaitingTime,
            _arbitrator
        );
        payments[_paymentID] = Payment({
            value: msg.value,
            createdAt: now,
            timeOut: _timeOut
        });
    }
}
```

]

???
For creating a payment, first, we create an agreement. Our composable arbitrable contracts use agreements as a generic abstraction over what comes before a dispute. In this case the agreement represents the payment and is initialized with all the required values. We then fill in the application specific data for the payment, which is just the value being sent and the data necessary to implement a time out.

---

## ERC 792 Arbitrable: Complex Example

### Two Party Arbitrable Escrow Payment: Payment Time Outs

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is MultiPartyInsurableArbitrableAgreementsBase {
    function executePayment(bytes32 _paymentID) external {
        Agreement storage agreement = agreements[_paymentID];
        Payment storage payment = payments[_paymentID];
        require(agreement.creator != address(0), "The specified payment does not exist.");
        require(!agreement.executed, "The specified payment has already been executed.");
        require(!agreement.disputed, "The specified payment is disputed.");
        require(now - payment.createdAt > payment.timeOut, "The specified payment has not timed out yet.");
        agreement.parties[1].send(payment.value); // Avoid blocking.
        agreement.executed = true;
        emit PaymentExecuted(_paymentID, agreement.parties[0], agreement.parties[1], payment.value);
    }
}
```

]

???
The process for executing a payment if a dispute was not raised during the time out is quite similar to the one in the simple example.

---

## ERC 792 Arbitrable: Complex Example

### Two Party Arbitrable Escrow Payment: Ruling Execution

.extra-small[

```sol
contract TwoPartyArbitrableEscrowPayment is MultiPartyInsurableArbitrableAgreementsBase {
    function executeAgreementRuling(bytes32 _agreementID, uint _ruling) internal {
        super.executeAgreementRuling(_agreementID, _ruling);
        Agreement storage agreement = agreements[_agreementID];
        PaidFees storage _paidFees = paidFees[_agreementID];
        Payment storage payment = payments[_agreementID];
        address _receiver;
        if (_paidFees.stake.length == 1) { // Failed to fund first round.
            // Send the value to whoever paid more.
            if (_paidFees.totalContributedPerSide[0][0] >= _paidFees.totalContributedPerSide[0][1]) _receiver = agreement.parties[0];
            else _receiver = agreement.parties[1];
        } else { // Failed to fund a later round.
            // Respect the ruling unless the losing side funded the appeal and the winning side paid less than expected.
            if (
                _paidFees.loserFullyFunded[_paidFees.loserFullyFunded.length - 1] &&
                _paidFees.totalContributedPerSide[_paidFees.totalContributedPerSide.length - 1][0] - _paidFees.stake[_paidFees.stake.length - 1] > _paidFees.totalContributedPerSide[_paidFees.totalContributedPerSide.length - 1][1]
            ) _receiver = agreement.parties[_ruling == 2 ? 0 : 1];
            else _receiver = agreement.parties[_ruling == 2 ? 1 : 0];
        }
        _receiver.send(payment.value); // Avoid blocking.
        agreement.executed = true;
        emit PaymentExecuted(_agreementID, agreement.parties[0], _receiver, payment.value);
    }
}
```

]

???
Ruling is more complex now, because we are taking fee payments into account. If the payment timed out while raising funds for the first round of the dispute, we send the payment to whoever raised more funds. If the dispute timed out while raising funds for a later round, we respect the ruling unless the losing side succesfuly funded his part of the fees and the winner didn't, in which case we reverse the ruling.

---

## ERC 792 Arbitrable: Other Examples

???
Now that you have a better understanding of what arbitrable contracts are, think about all the possible applications of the arbitration standard. We can make:

--

- Arbitrable contracts that binarize multiple choice disputes for binary arbitrators.

--

- Linear oracles that recursively ask an arbitrator if the correct answer lies under or over the median of suggested values, until it reaches the desired precision.

--

- Curated lists with a dispute challenge mechanism for registrations and clearings.

--

See the [`kleros-interaction` repo](https://github.com/kleros/kleros-interaction) for more inspiration.

.responsive[![Kleros Tech Stack](https://datavizcatalogue.com/methods/images/top_images/SVG/network_diagram.svg)]

???
These three are just the ones we are actively researching. We are excited to see what other use cases the community comes up with.

---

class: impact
background-image: url(https://ucdffce48006b4016fbfa4d30b1b.previews.dropboxusercontent.com/p/thumb/AAMQ157A_a8G1d24qPufyJHERn7n64QPduRLR4a-YjdyTV6zzDv8RNrr2a5iE1rBK_cB9CNr8FiXO-RiKupjIYCPCHQHsgQqIeFHIWyFYpXIT3I2RXK_hNhEFOF0LkQAplwKvCpkXoeZVaw60Oj8N-z5FkZvDeT4Ha1qvBUI6GRNNU1VwMPZCm9pzdmC9KrViMAxmRjvtb83RtjzBu4DO3xoY2IEVGxlMV6LvNI2VXnU6g/p.jpeg?size=2048x1536&size_mode=3)

# {{title}}

## Now it's your turn.

???
You have X minutes to come up with and code the most innovative arbitrable contract you can think of. We will go around the room helping and answering questions. When time is up, every team will present their creations and our jury will decide who wins the prize. Good luck and have fun!
